/*
PHIL {SFX} FISHER - 2005

E-MAIL: MOHAA.MODS@NTLWORLD.COM

WEBSITE: WWW.MODS-R-US.NET
*/


/*

Usage: exec global/TNT_DETONATORS.scr::DETONATOR <origin> <angles> before level waittill spawn. 


IE: map dm/mohdm1: exec global/TNT_DETONATORS.scr::DETONATOR ( 298.64 475.75 384.13 ) ( 0 0 0 )

To use press use on static plunger, then anywhere in map hold use for 3 seconds to plant bomb, then hold use anywhere for 3 
seconds again to drop plunger. Press use on plunger to setoff your bomb. Planter can only use plunger !

*/


DETONATOR local.D_ORIGIN local.D_ANGLES:

             local.FAKE_DETONATOR = spawn script_model
             local.FAKE_DETONATOR model "models/miscobj/detonator.tik"
             local.FAKE_DETONATOR.origin = local.D_ORIGIN
             local.FAKE_DETONATOR.angles = local.D_ANGLES
             local.FAKE_DETONATOR rotatey 55
             local.FAKE_DETONATOR notsolid

             local.FAKE_DETONATOR_TRIGGER = spawn trigger_use
             local.FAKE_DETONATOR_TRIGGER.origin = local.D_ORIGIN
             local.FAKE_DETONATOR_TRIGGER.angles = local.D_ANGLES
             local.FAKE_DETONATOR_TRIGGER setsize "-20 -20 0" "20 20 20"
             local.FAKE_DETONATOR_TRIGGER setthread GIVE_DETONATOR
             local.FAKE_DETONATOR_TRIGGER message "DETONATOR & TNT OBTAINED!"
             end

GIVE_DETONATOR:
             
             local.PLAYER = parm.other

             if (local.PLAYER.OWNS==1)
             {
               local.PLAYER iprint "YOU ARE A DOG, YOU CANT USE EXPLOSIVES!" 1   // PHIL {SFX} SMART HOUND PROTECTION
               end
             }

             if ((local.PLAYER.ARMED==1) || (local.PLAYER.DETONATORED==1))
             {
               local.PLAYER iprint "YOU HAVE A DETONATOR & TNT ALREADY!" 1
               end
             }

             if ((local.PLAYER.NF==1) && (local.PLAYER.ARMED==0) && (local.PLAYER.DETONATORED==0))
             {
               local.PLAYER iprint "YOU HAVE NOT USED THE TNT YOU HAVE PLACED ALREADY!" 1
               end
             }

             local.PLAYER iprint "HOLD USE TO PLANT TNT!" 1

             local.PLAYER.DEATH_SPEC = 0
             local.PLAYER.ARMED = 1
             local.PLAYER.DETONATORED = 1
             local.PLAYER.NF = 1

             thread MONITOR_ARMED local.PLAYER
             thread DEAD_OR_ALIVE local.PLAYER
             end

MONITOR_ARMED local.PLAYER:

             while ((isalive local.PLAYER) && (local.PLAYER.ARMED==1) && (local.PLAYER.DETONATORED==1))
             {
               if (local.PLAYER.useheld==1)
               {
                wait 1
                if (local.PLAYER.useheld==1)
                {
                 local.EXPLOSIVE = spawn script_model
                 local.EXPLOSIVE model "models/items/explosive2.tik"
                 local.EXPLOSIVE physics_on
                 local.EXPLOSIVE scale 2
                 local.EXPLOSIVE.origin = local.PLAYER.origin - local.PLAYER.forwardvector * -40 + ( 0 0 2 )
                 local.EXPLOSIVE notsolid

                 local.EXPLOSIVE playsound plantbomb1
                 wait 0.1
                 local.EXPLOSIVE playsound radiobomb

                 thread MONITOR_EXPLOSIVE local.PLAYER local.EXPLOSIVE

                 local.PLAYER iprint "TNT PLANTED!" 1
                 local.PLAYER iprint "HOLD USE TO PLACE THE DETONATOR!" 1
                 local.PLAYER.ARMED = 0
               }
             }
               wait 0.1
             }
             end

MONITOR_EXPLOSIVE local.PLAYER local.EXPLOSIVE:

              while ((isalive local.PLAYER) && (local.PLAYER.DETONATORED==1))
              {
                 if (local.PLAYER.useheld==1)
                 {
                   wait 1
                   if (local.PLAYER.useheld==1)
                   {
                     local.DETONATOR = spawn script_model
                     local.DETONATOR model "models/miscobj/detonator.tik"
                     local.DETONATOR physics_on
                     local.DETONATOR.origin = local.PLAYER.origin - local.PLAYER.forwardvector * -40 + ( 0 0 8 )
                     local.DETONATOR.angles = ( 0 180 0 )
                     local.DETONATOR notsolid

                     local.DETONATOR.EXPLOSIVE = local.EXPLOSIVE

                     local.DETONATOR_TRIGGER = spawn trigger_use
                     local.DETONATOR_TRIGGER.origin = local.DETONATOR.origin
                     local.DETONATOR_TRIGGER.angles = local.DETONATOR.angles
                     local.DETONATOR_TRIGGER setsize "-16 -16 0" "16 16 40"

                     local.DETONATOR_TRIGGER bind local.DETONATOR

                     local.DETONATOR playsound plantbomb1                   

                     local.PLAYER iprint "DETONATOR READY!" 1
                     local.PLAYER iprint "PRESS USE ON DETONATOR TO DETONATE TNT!" 1

                     local.DETONATOR.DETONATOR_TRIGGER = local.DETONATOR_TRIGGER
                     thread DETONATOR_TRIGGER_CHECK local.PLAYER local.DETONATOR
                     local.PLAYER.DETONATORED = 0
                    } 
               }  
                 wait 0.1  
               }       
               end

DETONATOR_TRIGGER_CHECK local.PLAYER local.DETONATOR:

                   local.DETONATOR.DETONATOR_TRIGGER waittill trigger

                   local.KILLER = parm.other  

                   local.DETONATOR anim fire
                   local.DETONATOR playsound plunger
                   local.DETONATOR.EXPLOSIVE playsound activate
                   wait 0.5
                   local.DETONATOR.EXPLOSIVE hide

                   for (local.PNUM=1;local.PNUM <= $player.size;local.PNUM++)
                   {
                     if ((vector_within $player[local.PNUM].origin local.DETONATOR.EXPLOSIVE.origin 200) && ($player[local.PNUM].dmteam != local.PLAYER.dmteam))
                     {
                       $player[local.PNUM] damage local.KILLER 100 local.DETONATOR.EXPLOSIVE (0 0 0) (0 0 0) (0 0 0) 0 1 rocket -1
                     }
                   }

                   exec global/model.scr local.DETONATOR.EXPLOSIVE.origin "models/emitters/explosion_bombdirt.tik" 

                   local.DETONATOR.EXPLOSIVE playsound boom
                   local.DETONATOR.EXPLOSIVE delete
                   wait 2
                   local.DETONATOR.DETONATOR_TRIGGER delete
                   local.DETONATOR delete
                   local.PLAYER.DEATH_SPEC = 1
                   local.PLAYER.NF = 0
                   end

                   thread DETONATOR_TRIGGER_CHECK local.PLAYER local.DETONATOR
                   end  
                  
DEAD_OR_ALIVE local.PLAYER:

                    while ((isalive local.PLAYER) && (local.PLAYER.dmteam!="spectator"))
                    waitframe

                    if (local.PLAYER.DEATH_SPEC!=1)
                   {
                      local.PLAYER.NF = 0
                      local.PLAYER.ARMED = 0
                      local.PLAYER.DETONATORED = 0
                      // local.PLAYER.DETONATOR delete                                     
                      // local.DETONATOR.EXPLOSIVE delete
                      // local.PLAYER.DETONATOR_TRIGGER delete			
                    }      
                    end