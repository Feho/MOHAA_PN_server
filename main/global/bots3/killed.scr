// bots3
// by jv_map

main:
	local.yaw = self GetLocalYawFromVector self.fact.direction
	
	if(self.fact.location == -1)
	{
		// explosive death
		if(self.fact.damage >= 150.0)
		{
			self setmotionanim death_explosion_large
		}
		else
		{
			if (local.yaw > -45 && local.yaw < 45)
			{
				self setmotionanim death_explosion_back
			}
			else if (local.yaw > 45 && local.yaw < 135)
			{
				self setmotionanim death_explosion_right
			}
			else if (local.yaw > -135 && local.yaw < -45)
			{
				self setmotionanim death_explosion_left
			}
			else
			{
				self setmotionanim death_explosion_small
			}
		}
	}
	else
	{
		// 'normal' shot or club death
		if(self.velocity * self.forwardvector > 80.0)
		{
			if(local.yaw > -45 && local.yaw < 45 && self.fact.damage > 50.0)
			{
				if(randomfloat 1.0 > 0.3)
				{
					self setmotionanim death_chest
				}
				else
				{
					self setmotionanim death_knockedup
				}
			}
			else
			{
				self setmotionanim death_run
			}
		}
		else if(self.pose == level.bots.POSE_CROUCH)
		{
			if (local.yaw < -135 || local.yaw > 135)
			{
				self setmotionanim death_frontcrouch
			}
			else
			{
				self setmotionanim rifle_pain_kneestodeath
			}
		}
		else
		{
			switch(self.fact.location)
			{
				case 0: // head
				case 1: // helmet
					
					if(local.yaw > -45 && local.yaw < 45 && randomint 2 == 1)
					{
						self setmotionanim death_back
					}
					else if((local.yaw < -135 || local.yaw > 135) && randomint 2 == 1)
					{
						self setmotionanim death_head_flyforward
					}
					else
					{
						switch(randomint 5)
						{
							case 0:
								self setmotionanim death_headpistol
								break
							case 1:
								self setmotionanim death_twist
								break
							case 2:
								self setmotionanim death_shoot
								break
							case 3:
								self setmotionanim death_fall_to_knees
								break
							default:
								self setmotionanim death_collapse
						}
					}
					break
				
				case 2: // neck
					
					if(randomint 2 == 1)
					{
						self setmotionanim death_frontchoke
					}
					else
					{
						self setmotionanim death_choke
					}
					break
				
				case 3: // upper torso
				case 4: // middle torso
					
					if(local.yaw > -45 && local.yaw < 45 && randomint 5 == 1)
					{
						self setmotionanim death_knockedup
					}
					else if(local.yaw > -45 && local.yaw < 45 && randomint 2 == 1)
					{
						self setmotionanim death_back
					}
					else if((local.yaw < -135 || local.yaw > 135) && randomint 2 == 1)
					{
						self setmotionanim death_run
					}
					else if(randomint 3 == 1)
					{
						self setmotionanim death_frontchoke
					}
					else if(randomint 2 == 1)
					{
						self setmotionanim death_shoot
					}
					else
					{
						self setmotionanim death_chest
					}
					break
				
				case 5: // lower torso
				case 6: // pelvis
					
					if(local.yaw > -45 && local.yaw < 45 && randomint 2 == 1)
					{
						self setmotionanim death_back
					}
					else if((local.yaw < -135 || local.yaw > 135) && randomint 3 == 1)
					{
						self setmotionanim death_knockedup
					}
					else if((local.yaw < -135 || local.yaw > 135) && randomint 2 == 1)
					{
						self setmotionanim death_run
					}
					else if(randomint 2 == 1)
					{
						self setmotionanim death_crotch
					}
					else
					{
						self setmotionanim death_chest
					}
					break
				
				case 7:  // r upper arm
				case 11: // r lower arm
				case 15: // r hand
					
					if((local.yaw < -135 || local.yaw > 135) && randomint 2 == 1)
					{
						self setmotionanim death_twist
					}
					else if(randomint 3 == 1)
					{
						self setmotionanim death_fall_back
					}
					else if(randomint 2 == 1)
					{
						self setmotionanim death_shoot
					}
					else
					{
						self setmotionanim death_chest
					}
					break
				
				case 8:  // l upper arm
				case 12: // l lower arm
				case 16: // l hand
					if(local.yaw > -45 && local.yaw < 45 && randomint 2 == 1)
					{
						self setmotionanim death_twist
					}
					else if(randomint 3 == 1)
					{
						self setmotionanim death_fall_back
					}
					else if(randomint 2 == 1)
					{
						self setmotionanim death_shoot
					}
					else
					{
						self setmotionanim death_chest
					}
					break
					
				default: // leg or foot
					if((local.yaw < -135 || local.yaw > 135) && randomint 2 == 1)
					{
						self setmotionanim death_knockedup
					}
					else if(randomint 4 == 1)
					{
						self setmotionanim death_fall_back
					}
					else if(randomint 3 == 1)
					{
						self setmotionanim death_shoot
					}
					else if(randomint 2 == 1)
					{
						self setmotionanim death_fall_to_knees
					}
					else
					{
						self setmotionanim death_chest
					}
			}
		}
	}
	
	self waittill flaggedanimdone
end