// m1 rifle
// by jv_map

// let bots3 know I compiled ok.
acte_de_presence:
end 1

info:
	self.clipsize = 8
	self.weapontype = "rifle"
	self.spread = 0.009
	
	self.rounds_in_clip = self.clipsize
end

action:

	self playsound snd_rifle_pullout
	waitthread level.bots.anim::playupperanim rifle_stand_raise

	// can only exit this loop 
	// by dieing or changing weapon
	
	while(isAlive self)
	{
		if(self.new_weapon)
		{
			// holster rifle
			self playsound snd_rifle_putaway
			waitthread level.bots.anim::playupperanim rifle_stand_lower
			break
		}
		else if(self.current_weapon.rounds_in_clip <= 0)
		{
			// reload
			waitthread level.bots.anim::playupperanim rifle_reload
			if(isAlive self)
			{
				self.current_weapon.rounds_in_clip = self.current_weapon.clipsize
			}
		}
		// the randomfloat is a 'misclick' simulator
		else if(self.key_attack_primary && self readyToFire && randomfloat 1.0 < 0.3)
		{
			// fire
			waitthread level.bots.common::fire_current_weapon
		}
		else if(level.time - self.last_pain_time <= 0.05)
		{
			// ouch
			if(self.pose == level.bots.POSE_STAND)
			{
				waitthread level.bots.anim::playupperanim rifle_pain
			}
			else
			{
				waitthread level.bots.anim::playupperanim rifle_pain_ducked
			}
		}
		else
		{
			// no action
			waitframe
		}
	}
end